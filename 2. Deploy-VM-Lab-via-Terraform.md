## Overview
Deploys the previous VM lab using Terraform.
The Terraform folder structure
```
vm-lab/
 ├── main.tf
 ├── variables.tf
 └── outputs.tf 
```

---
## Terraform Config Files
The below will create the Terraform files and copy the contents into them.

```
mkdir -p ~/terraform/vm-lab/
```
### main.tf  
``` bash
cat << 'EOF' > ~/terraform/vm-lab/main.tf
terraform {
  required_version = ">= 1.5.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 4.0"
    }
  }
}

provider "azurerm" {
  features {}
  use_cli = true
  subscription_id = "6908ec90-4b9c-4100-aa00-4822de4f4e4b"
  resource_provider_registrations = "none"
}

resource "azurerm_resource_group" "lab" {
  name     = "RG-Terraform-VM-Lab"
  location = "australiaeast"
}

resource "azurerm_virtual_network" "vnet" {
  name                = "vnet-lab"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
}

resource "azurerm_subnet" "subnet_jump" {
  name                 = "subnet-jump"
  resource_group_name  = azurerm_resource_group.lab.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]
}

resource "azurerm_subnet" "subnet_web" {
  name                 = "subnet-web"
  resource_group_name  = azurerm_resource_group.lab.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.2.0/24"]
}

resource "azurerm_network_security_group" "nsg_jump" {
  name                = "nsg-jump"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
}

resource "azurerm_network_security_group" "nsg_web" {
  name                = "nsg-web"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
}

resource "azurerm_network_security_rule" "jump_ssh" {
  name                        = "Allow-SSH"
  priority                    = 1000
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "22"
  source_address_prefixes     = var.home_ip
  destination_address_prefix  = "*"
  resource_group_name         = azurerm_resource_group.lab.name
  network_security_group_name = azurerm_network_security_group.nsg_jump.name
}

resource "azurerm_network_security_rule" "jump_icmp" {
  name                        = "Allow-VNet-Ping"
  priority                    = 1100
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Icmp"
  destination_address_prefix  = "*"
  destination_port_range      = "*"
  source_address_prefix       = "10.0.0.0/16"
  source_port_range           = "*"
  resource_group_name         = azurerm_resource_group.lab.name
  network_security_group_name = azurerm_network_security_group.nsg_jump.name
}

resource "azurerm_network_security_rule" "web_ssh_from_jump" {
  name                        = "Allow-SSH-From-Jump"
  priority                    = 100
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  destination_address_prefix  = "*"
  destination_port_range      = "22"
  source_address_prefix       = "10.0.1.0/24"
  source_port_range           = "*"
  resource_group_name         = azurerm_resource_group.lab.name
  network_security_group_name = azurerm_network_security_group.nsg_web.name
}

resource "azurerm_network_security_rule" "web_http" {
  name                        = "Allow-HTTP"
  priority                    = 200
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  destination_address_prefix  = "*"
  destination_port_range      = "80"
  source_address_prefix       = "Internet"
  source_port_range           = "*"
  resource_group_name         = azurerm_resource_group.lab.name
  network_security_group_name = azurerm_network_security_group.nsg_web.name
}

resource "azurerm_subnet_network_security_group_association" "jump_assoc" {
  subnet_id                 = azurerm_subnet.subnet_jump.id
  network_security_group_id = azurerm_network_security_group.nsg_jump.id
}

resource "azurerm_subnet_network_security_group_association" "web_assoc" {
  subnet_id                 = azurerm_subnet.subnet_web.id
  network_security_group_id = azurerm_network_security_group.nsg_web.id
}

resource "azurerm_public_ip" "jump_pip" {
  name                = "pip-jump"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
  allocation_method   = "Static"
  sku                 = "Standard"
}

resource "azurerm_public_ip" "web_pip" {
  name                = "pip-web"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
  allocation_method   = "Static"
  sku                 = "Standard"
}

resource "azurerm_network_interface" "nic_jump" {
  name                = "nic-jump"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.subnet_jump.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.jump_pip.id
  }
}

resource "azurerm_network_interface" "nic_web" {
  name                = "nic-web"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.subnet_web.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.web_pip.id
  }
}

resource "azurerm_linux_virtual_machine" "vm_jump" {
  name                = "vm-jump"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
  size                = "Standard_B2s"
  admin_username      = "azureadmin"
  network_interface_ids = [
    azurerm_network_interface.nic_jump.id
  ]

  admin_ssh_key {
    username   = "azureadmin"
    public_key = file("~/.ssh/azure_key.pub")
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "StandardSSD_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts"
    version   = "latest"
  }
}

resource "azurerm_linux_virtual_machine" "vm_web" {
  name                = "vm-web"
  location            = azurerm_resource_group.lab.location
  resource_group_name = azurerm_resource_group.lab.name
  size                = "Standard_B2s"
  admin_username      = "azureadmin"
  network_interface_ids = [
    azurerm_network_interface.nic_web.id
  ]

  admin_ssh_key {
    username   = "azureadmin"
    public_key = file("~/.ssh/azure_key.pub")
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "StandardSSD_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts"
    version   = "latest"
  }
}
EOF
```

### variables.tf
```bash
cat << 'EOF' > ~/terraform/vm-lab/variables.tf
variable "home_ip" {
  description = "Your public IP for SSH access"
  type        = list(string)
  default     = ["103.19.4.146", "114.31.212.161"]
}
EOF
```

### outputs.tf
``` bash
cat << 'EOF' > ~/terraform/vm-lab/outputs.tf
output "jumpbox_public_ip" {
  value = azurerm_public_ip.jump_pip.ip_address
}

output "web_private_ip" {
  value = azurerm_network_interface.nic_web.private_ip_address
}
EOF
```

---
## Deploying

```
cd ~/terraform/vm-lab
terraform init
terraform validate
terraform plan
terraform apply
```

---
## Testing

Update the ssh config then
``` bash
ssh web
```

``` bash
sudo apt update && sudo apt install nginx -y
```

``` bash
sudo systemctl status nginx
```

Navigate to the public IP and confirm you can see the nginx page

---
## Cleanup

The lab can be removed with 
```
terraform destroy
```